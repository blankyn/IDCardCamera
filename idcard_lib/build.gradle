import java.util.regex.Pattern

plugins {
    id 'com.android.library'
}

apply from: '../idcard_lib/bintray.gradle'


/**
 * 获取maven版本，默认取工程属性字段maven_version,
 * 若空，则选择项目配置的MAVEN_VERSION_NAME
 * @return
 */
def getMavenVersion() {
    if (rootProject.hasProperty("MAVEN_VERSION")
            && MAVEN_VERSION != null && "" != MAVEN_VERSION) {
        return String.valueOf(MAVEN_VERSION)
    } else {
        return '1.0.0'
    }
}

def checkVersion(String version) {
    Pattern versionPattern = Pattern.compile("^[0-9].[0-9](.[0-9])?(-SNAPSHOT)?")
    if (!versionPattern.matcher(version).matches()) {
        throw new Exception("${project.name}版本号验证错误${version}\nrelease版本规则：x.x.x(x)(.xx)\nsnapshot版本规则：x.x.x(x)(.xx)-SNAPSHOT")
    }
}

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionName getMavenVersion()
        checkVersion(versionName)
        def vCode = versionName.replace(".", "").replace("-SNAPSHOT", "")
        versionCode Integer.parseInt(vCode)
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'consumer-rules.pro'
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    compileOnly 'androidx.appcompat:appcompat:1.2.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    def camerax_version = "1.1.0-beta01"

    compileOnly "androidx.camera:camera-core:${camerax_version}"
    compileOnly "androidx.camera:camera-camera2:${camerax_version}"
    compileOnly "androidx.camera:camera-lifecycle:${camerax_version}"

    compileOnly "androidx.camera:camera-view:${camerax_version}"
    compileOnly "androidx.camera:camera-extensions:${camerax_version}"

}
